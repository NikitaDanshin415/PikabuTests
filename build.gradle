plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
}

group 'com.demowebshop'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    allureVersion = '2.17.3'
    junitVersion = '5.8.2'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation('org.projectlombok:lombok:1.18.22')
    testImplementation('com.codeborne:selenide:6.4.0')
    testImplementation("io.qameta.allure:allure-selenide:${allureVersion}")
    testImplementation('org.assertj:assertj-core:3.22.0')
    testImplementation('org.slf4j:slf4j-simple:1.7.36')
    testImplementation('org.aeonbits.owner:owner:1.0.12')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

allure {
    report {
        version.set("2.17.3")
    }
    adapter {
        aspectjWeaver.set(true) 
        frameworks {
            junit5 {
                adapterVersion.set("2.17.3")
            }
        }
    }
}

tasks.withType(Test) {
    systemProperties(System.getProperties())

    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task parallelRegress(type: Test) {
    useJUnitPlatform {
        includeTags 'regress'
    }
    maxParallelForks = System.properties['forkCount'] ? System.properties['forkCount'].toInteger() : 1
}
